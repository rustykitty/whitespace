%import common.SIGNED_INT
%import common.WS
%import common.CNAME

%ignore WS

// derived from the C99 grammar

HEXADECIMAL_DIGIT: /[0-9A-Fa-f]/
HEX_QUAD: HEXADECIMAL_DIGIT HEXADECIMAL_DIGIT HEXADECIMAL_DIGIT HEXADECIMAL_DIGIT

C_CHAR: /[^'\\\n]/ | ESCAPE_SEQUENCE
ESCAPE_SEQUENCE: SIMPLE_ESCAPE | OCTAL_ESCAPE | HEXADECIMAL_ESCAPE | UNIVERSAL_CHARACTER_NAME
SIMPLE_ESCAPE: /\\['"?\\abfnrtv]/
OCTAL_ESCAPE: /\\[0-7]{1,3}/
HEXADECIMAL_ESCAPE: "\\x" HEXADECIMAL_DIGIT+
UNIVERSAL_CHARACTER_NAME: "\\u" HEX_QUAD | "\\U" HEX_QUAD HEX_QUAD

CHARACTER_LITERAL: "'" C_CHAR "'"


NEWLINE: "\n" | "\r\n" 

program: (NEWLINE* statement)* NEWLINE*

NUMBER_LITERAL: SIGNED_INT
LABEL_LITERAL: CNAME

COMMENT: /;[^\n]*(\r\n)?/ // adapted from common.SH_COMMENT

instruction: "push" -> push
           | "dup" -> dup
           | "copy" -> copy
           | "swap" -> swap
           | "pop" -> pop
           | "slide" -> slide
           | "add" -> add
           | "sub" -> sub
           | "mul" -> mul
           | "div" -> div
           | "mod" -> mod
           | "store" -> store
           | "load" -> load
           | "label" -> label
           | "jmp" -> jmp
           | "jz" -> jz
           | "jlz" -> jlz
           | "call" -> call
           | "ret" -> ret
           | "end" -> end
           | "outchr" -> outchr
           | "outnum" -> outnum
           | "inchr" -> inchr
           | "innum" -> innum

statement: instruction literal?

?literal: NUMBER_LITERAL -> number_literal
        | LABEL_LITERAL -> label_literal
        | CHARACTER_LITERAL -> char_literal

%ignore COMMENT