program: statement*
number_literal: /[ \t][ \t]+\n/
label_literal: /[ \t]+\n/
statement: 
    | "  "      number_literal -> push
    | " \n "     -> dup
    | " \t "    number_literal -> copy
    | " \n\t"    -> swap
    | " \n\n"    -> pop
    | " \t\n"   number_literal -> slide
    | "\t   "    -> add
    | "\t  \t"   -> sub
    | "\t  \n"   -> mul
    | "\t \t "   -> div
    | "\t \t\t"  -> mod
    | "\t\t "    -> store
    | "\t\t\t"   -> load
    | "\n  "    label_literal -> label
    | "\n \t"   label_literal -> call
    | "\n \n"   label_literal -> jmp
    | "\n\t "   label_literal -> jz
    | "\n\t\t"  label_literal -> jlz // as in "jump if less than zero"
    | "\n\t\n"   -> ret
    | "\n\n\n"   -> end
    | "\t\n  "   -> outchr
    | "\t\n \t"  -> outnum
    | "\t\n\t\n" -> inchr
    | "\t\n\t\t" -> innum

%ignore /[^ \t\n]/